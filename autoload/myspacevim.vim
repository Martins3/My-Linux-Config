func! myspacevim#before() abort
  " 当文件被其他编辑器修改时，自动加载
  set autoread
  au FocusGained,BufEnter * :checktime

  " 当失去焦点或者离开当前的 buffer 的时候保存
  set autowrite
  autocmd FocusLost,BufLeave * silent! update

  " 重新映射 leader 键
  let g:mapleader = ','

  " 重新映射 window 键位
  let g:spacevim_windows_leader = 'c'

  let g:table_mode_corner='|'

  " 调节 window 大小
  let g:winresizer_start_key = '<space>wa'

  " If you cancel and quit window resize mode by `q` (keycode 113)
  let g:winresizer_keycode_cancel = 113

  " 让file tree 显示文件图标，需要 terminal 安装 nerd font
  let g:spacevim_enable_vimfiler_filetypeicon = 1

  " 让 filetree 显示 git 的状态
  " let g:spacevim_enable_vimfiler_gitstatus = 1

  " 默认 markdown preview 在切换到其他的 buffer 或者 vim
  " 失去焦点的时候会自动关闭 preview，让
  let g:mkdp_auto_close = 0

  " 书签选中之后自动关闭 quickfix window
  let g:bookmark_auto_close = 1
  " 设置一个删除所有的 bookmark 的快捷键
  call SpaceVim#custom#SPC('nnoremap', ['b', 'C'], 'BookmarkClearAll', 'clear all bookmark', 1)

  " ctrl + ] 查询 cppman
  " 如果想让该快捷键自动查询 man，将Cppman 替换为 Cppman!
  autocmd FileType c,cpp noremap <C-]> <Esc>:execute "Cppman " . expand("<cword>")<CR>

  " Tell Neosnippet about the other snippets
  " ref : https://github.com/Shougo/neosnippet.vim
  let g:neosnippet#snippets_directory='~/.cache/vimfiles/repos/github.com/honza/vim-snippets/snippets'

  " 让光标自动进入到popup window 中间
  let g:git_messenger_always_into_popup = v:true
  " 设置映射规则，和 spacevim 保持一致
  call SpaceVim#custom#SPC('nnoremap', ['g', 'm'], 'GitMessenger', 'show commit message in popup window', 1)
  call SpaceVim#custom#SPC('nnoremap', ['g', 'l'], 'FloatermNew lazygit', 'open lazygit in floaterm', 1)

  " 和 sourcetrail 配合使用
  call SpaceVim#custom#SPC('nnoremap', ['a', 'a'], 'SourcetrailStartServer', 'start sourcetrail server', 1)
  call SpaceVim#custom#SPC('nnoremap', ['a', 'b'], 'SourcetrailActivateToken', 'sync sourcetrail with neovim', 1)
  call SpaceVim#custom#SPC('nnoremap', ['a', 'f'], 'SourcetrailRefresh', 'sourcetrail server', 1)

  " 设置默认的pdf阅览工具
  let g:vimtex_view_method = 'zathura'
  let g:vimtex_syntax_conceal_default = 0
  " 关闭所有隐藏设置
  let g:tex_conceal = ""

  " 实现一键运行各种文件，适合非交互式的，少量的代码，比如 leetcode
  func! QuickRun()
    exec "w"
    let ext = expand("%:e")

    if ext ==# "sh"
      exec "!bash %"
    elseif ext ==# "cpp"
      let out = expand('%:p') . ".out"
      exec "!clang++ % -Wall -lpthread -g -std=c++17 -o " . out . " && " . out
    elseif ext ==# "c"
      let out = expand('%:p') . ".out"
      exec "!clang % -Wall -lpthread -g -std=c11 -o " . out . " && " . out
    elseif ext ==# "java"
      let classPath = expand('%:h')
      let className = expand('%:p:t:r')
      " echo classPath
      " echo className
      exec "!javac %"
      exec "!java -classpath " . classPath . " " . className
    elseif ext ==# "go"
      exec "!go run %"
    elseif ext ==# "js"
      exec "!node %"
    elseif ext ==# "bin"
      exec "!readelf -h %"
    elseif ext ==# "py"
      exec "!python3 %"
    elseif ext ==# "vim"
      exec "so %"
    elseif ext ==# "html"
      exec "!microsoft-edge %"
    elseif ext ==# "rs"
      call CargoRun()
    elseif ext ==# "svg"
      exec "%!xmllint --format -"
    else
      echo "Check file type !"
    endif
    echo 'done'
  endf

  " 一键运行 rust 工程，不断向上查找直到遇到 Cargo.toml，然后执行 cargo run
  func! CargoRun()
    let cargo_run_path = fnamemodify(resolve(expand('%:p')), ':h')
    while cargo_run_path != "/"
      if filereadable(cargo_run_path . "/Cargo.toml")
        echo cargo_run_path
        exec "cd " . cargo_run_path
        exec "!cargo run"
        exec "cd -"
        return
      endif
      let cargo_run_path = fnamemodify(cargo_run_path, ':h')
    endwhile
    echo "Cargo.toml not found !"
  endf

  " floaterm
  let g:floaterm_keymap_prev   = '<C-p>'
  let g:floaterm_keymap_new    = '<C-n>'
  let g:floaterm_keymap_toggle = '<F5>'
endf

func! myspacevim#after() abort
  " <F3> 打开文件树
  let g:vista_sidebar_position = "vertical topleft"
  let g:vista_default_executive = 'coc'
  let g:vista_finder_alternative_executives = 'ctags'
  nnoremap  <F2>  :Vista!!<CR>
  nnoremap  <F4>  :call QuickRun()<CR>
  " <F5> floaterm toggle
  " <F7> 打开历史记录
  tnoremap  <Esc>  <C-\><C-n>

  " 使用 tab 切换 window
  map <Tab> :wincmd w<CR>

  " press <esc> to cancel.
  nmap f <Plug>(coc-smartf-forward)
  nmap F <Plug>(coc-smartf-backward)
  nmap ; <Plug>(coc-smartf-repeat)
  nmap , <Plug>(coc-smartf-repeat-opposite)
endf
